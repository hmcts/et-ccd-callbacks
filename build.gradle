plugins {
    id 'application'
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.springframework.boot' version '2.7.12'
    id 'org.owasp.dependencycheck' version '8.2.1'
    id 'com.github.ben-manes.versions' version '0.46.0'
    id 'au.com.dius.pact' version '4.3.15'
    id 'io.freefair.lombok' version '8.0.1'
    id 'org.sonarqube' version '4.0.0.2929'
    id 'pmd'
    id 'com.github.hmcts.rse-cft-lib' version '0.19.670'
}

group = 'uk.gov.hmcts.et'
version = '0.0.1'

pmd {
    toolVersion = "6.41.0"
    sourceSets = [sourceSets.main, sourceSets.test, sourceSets.cftlib]
    reportsDir = file("$project.buildDir/reports/pmd")
    // https://github.com/pmd/pmd/issues/876
    ruleSets = []
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

checkstyle {
    maxWarnings = 0
    toolVersion = '10.10.0'
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

def sonarExclusions = ['**/uk/gov/hmcts/ethos/replacement/docmosis/config/**',
                       '**/uk/gov/hmcts/ethos/replacement/docmosis/model/**',
                       '**/uk/gov/hmcts/reform/ethos/replacement/docmosis/CaseOrchestrationApplication',
                       '**/uk/gov/hmcts/ethos/replacement/docmosis.config.DevQueueClient.java',
                       '**/uk/gov/hmcts/ethos/replacement/docmosis/domain/admin/**'
]

sonarqube {
    properties {
        property "sonar.exclusions", sonarExclusions.join(', ')
        property "sonar.projectKey", "et-ccd-callbacks"
        property "sonar.dynamicAnalysis", "reuseReports"
        property "sonar.dependencyCheck.reportPath", "${project.buildDir}/reports/dependency-check-report.xml"
        property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
    }
}

sourceSets {
    test {
        java.srcDir 'src/test/java'
    }
    functional {
        java.srcDir 'src/test/functional/java'
        resources.srcDir 'src/test/functional/resources'
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
    contractTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            java.srcDir 'src/test/contractTest/java'
            resources.srcDir 'src/test/contractTest/resources'
        }
        resources.srcDir file('src/test/contractTest/resources')
    }
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/test/integration/java')
        }
        resources.srcDir file('src/test/integration/resources')
    }
}

configurations {
    functionalImplementation.extendsFrom testImplementation
    functionalRuntimeOnly.extendsFrom testRuntimeOnly
    contractTestImplementation.extendsFrom testImplementation
    contractTestRuntimeOnly.extendsFrom testRuntimeOnly
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

test {
    exclude '**/*ComponentTest*'
    useJUnitPlatform()
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification', dependsOn: ':codeceptSmoke') {
    testClassesDirs = sourceSets.functional.output.classesDirs
    classpath = sourceSets.functional.runtimeClasspath

    useJUnit {
        includeCategories 'uk.gov.hmcts.ethos.replacement.functional.SmokeTest'
    }
    systemProperty('VAULTNAME', System.getenv('VAULTNAME'))
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification', dependsOn: ':codeceptFunctional') {
    testClassesDirs = sourceSets.functional.output.classesDirs
    classpath = sourceSets.functional.runtimeClasspath

    useJUnit {
        includeCategories 'uk.gov.hmcts.ethos.replacement.functional.SmokeTest'
    }

    systemProperty('VAULTNAME', System.getenv('VAULTNAME'))
    maxHeapSize = '1G'
}

task yarnInstall(type: Exec) {
    workingDir '.'
    commandLine 'yarn', 'install'
}

task yarnAudit(type: Exec,  dependsOn: ':yarnInstall') {
    workingDir '.'
    commandLine 'yarn', 'npm', 'audit', '--groups', 'dependencies', '--json', '|', 'grep', 'auditAdvisory', '>', 'yarn-audit-known-issues'
}

task codeceptSmoke(type: Exec, dependsOn: ':yarnInstall') {
    commandLine './node_modules/codeceptjs/bin/codecept.js', 'run', '-c', './src/test/end-to-end/', '--grep', '@smoke', '--steps', '--reporter', 'mocha-multi'
}

task codeceptFunctional(type: Exec, dependsOn :'yarnInstall') {
    commandLine './node_modules/codeceptjs/bin/codecept.js', 'run', '-c', './src/test/end-to-end/codecept.conf.js', '--grep', '@nightly', '--steps', '--reporter', 'mocha-multi', '--verbose'
}

task crossbrowser(dependsOn: ':codeceptFunctionalSauce') {
    group = 'Delivery pipeline'
    description = 'Execute cross browser tests on saucelabs'
}

task nightlyFunctional(dependsOn: ':codeceptFunctionalNightly') {
    group = 'Delivery pipeline'
    description = 'Execute Functional Tests on nightly pipeline'
}

task codeceptFunctionalSauce(type: Exec, dependsOn: ':yarnInstall') {
    //IGNORE_SESSION_VALIDATION=true NODE_ENV=testing LOG_LEVEL=OFF NODE_PATH=. node_modules/.bin/codeceptjs run-multiple  ${BROWSER_GROUP:-'--all'} -c src/test/end-to-end/saucelabs.conf.js --grep @RET-XB --steps --reporter mocha-multi
    workingDir '.'
    commandLine './bin/run-crossbrowser-tests.sh'
    commandLine 'node_modules/.bin/codeceptjs', 'run-multiple', '--all', '-c', './src/test/end-to-end/saucelabs.conf.js', '--grep', '@crossbrowser', '--reporter', 'mocha-multi', '--verbose'
}

task codeceptFunctionalNightly(type: Exec, dependsOn :'yarnInstall') {
    commandLine './node_modules/codeceptjs/bin/codecept.js', 'run', '-c', './src/test/end-to-end/codecept.conf.js', '--grep', '@nightly', '--reporter', 'mocha-multi'
}

task integration(type: Test) {
    description = "Runs integration tests"
    group = "Verification"
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    failFast = true
    reports {
        html.enabled(true)
        html.destination file("${buildDir}/integration/")
    }
}

project.tasks['check'].dependsOn integration

project.ext {
    pacticipant = 'ethos_replDocmosisService'
    pacticipantVersion = getCheckedOutGitCommitHash()
}

def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --shirt HEAD'.execute().text.trim()
}

task runContractTests(type: Test, description: 'Runs contract tests.', group: 'Verification') {
    onlyIf {
        return System.env.SKIP_CONTRACT_TESTS == null || System.env.SKIP_CONTRACT_TESTS == 'false'
    }
    useJUnitPlatform()
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    systemProperty 'pact.rootDir', "pacts"
    testLogging.showStandardStreams = true
}

task runAndPublishConsumerPactTests(type: Test, description: 'Publish pact test to broker.', group: 'Verification') {
    logger.lifecycle("Runs pact consumer Tests")

    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
}

rootProject.tasks.named("processContractTestResources") {
    duplicatesStrategy = 'include'
}

runAndPublishConsumerPactTests.finalizedBy pactPublish

pact {
    broker {
        pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
    }
    publish {
        pactDirectory = 'pacts'
        tags = [System.getenv("PACT_BRANCH_NAME") ?: 'Dev']
        version = project.pacticipantVersion
    }
}

task fortifyScan(type: JavaExec)  {
    main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

jacocoTestReport {
    executionData(test, integration)
    reports {
        xml.required = true
        csv.required = false
        xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.0
            }
        }
    }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'config/owasp/suppressions.xml'
    analyzers.assemblyEnabled = false
    analyzers.nodeAudit.yarnEnabled = false
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
check.dependsOn integration
checkstyleMain.shouldRunAfter(compileJava)
test.shouldRunAfter(checkstyleTest)
integration.shouldRunAfter(checkstyleIntegrationTest)
integration.shouldRunAfter(test)

ext {
    junitJupiterVersion = '5.9.3'
    log4JVersion = '2.20.0'
    pact_version = '4.3.10'
    serenity = '3.7.1'
    tomcatEmbedVersion = '9.0.75'
}

ext["rest-assured.version"] = '5.1.0'

dependencyManagement {
    dependencies {
        dependencySet(group: 'com.google.guava', version: '31.1-jre') {
            entry 'guava'
        }
        dependencySet(group: 'io.netty', version: '4.1.92.Final') {
            entry 'netty-buffer'
            entry 'netty-codec'
            entry 'netty-codec-http'
            entry 'netty-codec-socks'
            entry 'netty-common'
            entry 'netty-handler'
            entry 'netty-handler-proxy'
            entry 'netty-resolver'
            entry 'netty-transport'
            entry 'netty-transport-native-epoll'
            entry 'netty-transport-native-kqueue'
            entry 'netty-transport-native-unix-common'
        }
    }
}

dependencies {

    implementation group: 'com.github.hmcts', name: 'et-common', version:'1.0.14'
    implementation group: 'com.github.hmcts', name: 'et-data-model', version:'2.2.1'
    implementation group: 'com.github.hmcts', name: 'ccd-case-document-am-client', version: '1.7.3'
    implementation group: 'com.github.hmcts', name: 'document-management-client', version: '7.0.1'
    implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '5.1.9'
    implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.31'

    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    implementation group: 'io.netty', name: 'netty-tcnative-classes', version: '2.0.60.Final'

    implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.23.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
    implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion
    implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.28'
    implementation group: 'org.apache.santuario', name: 'xmlsec', version: '3.0.2'
    implementation group: 'com.fasterxml.woodstox', name: 'woodstox-core', version: '6.5.0'
    implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: tomcatEmbedVersion
    implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: tomcatEmbedVersion
    implementation group: 'org.springframework.security', name: 'spring-security-crypto', version: '5.7.8'
    //to remove CVEs
    implementation group: 'org.apache.xmlgraphics', name: 'batik-all', version: '1.16', ext: 'pom'
    //To Remove vulnerability CVE-2020-11988
    implementation group: 'org.apache.xmlgraphics', name: 'xmlgraphics-commons', version: '2.8'
    implementation group: 'org.flywaydb', name: 'flyway-core', version: '9.17.0'
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.6.0'
    implementation group: 'org.slf4j', name: 'slf4j-ext', version: '2.0.7'
    implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.6.15'
    implementation 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '3.1.7'
    implementation group: 'org.springframework.retry', name: 'spring-retry', version: '1.3.3'
    // CVE-2022-25857
    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.0'
    implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '3.19.1-RELEASE'
    implementation group: 'commons-validator', name: 'commons-validator', version: '1.7'
    // 1084765
    implementation group: 'org.webjars.npm', name: 'minimatch', version: '5.1.6'
    implementation group: 'org.asynchttpclient', name: 'async-http-client-netty-utils', version: '2.12.3'
    implementation group: 'io.netty', name: 'netty-transport-classes-epoll', version: '4.1.92.Final'
    implementation group: 'io.netty', name: 'netty-transport-classes-kqueue', version: '4.1.92.Final'

    implementation group: 'com.fasterxml.woodstox', name: 'woodstox-core', version: '6.5.1'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'com.github.hmcts:fortify-client:1.3.0:all', {
        exclude module: 'commons-io'
    }

    testImplementation group: 'com.auth0', name: 'java-jwt', version: '4.4.0'
    testImplementation group: 'com.warrenstrange', name: 'googleauth', version: '1.5.0'
    testImplementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    testImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: serenity
    testImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: serenity
    testImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: serenity, {
        exclude module: 'jaxb-osgi'
    }
    testImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: serenity
    testImplementation (group: 'org.docx4j', name: 'docx4j', version: '6.1.2') {
        exclude module: 'slf4j-log4j12'
    }
    testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: junitJupiterVersion
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testImplementation group: 'org.testcontainers', name: 'postgresql', version: '1.18.0'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.24.2'

    // Contract Tests
    contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: '4.5.6'
    contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'java8', version: '4.1.41'
    contractTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.7.11'

    contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
    contractTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion
    contractTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.9.3'
    contractTestImplementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.0.2'
    contractTestImplementation group: 'org.springframework.cloud', name: 'spring-cloud-netflix-ribbon', version: '2.2.10.RELEASE'
    contractTestImplementation sourceSets.main.runtimeClasspath
    contractTestImplementation sourceSets.test.runtimeClasspath

    integrationTestImplementation group: 'commons-io', name: 'commons-io', version:  '2.11.0'
    integrationTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.26'

    cftlibImplementation group: 'org.mockito', name: 'mockito-core', version: '5.3.1'
    cftlibImplementation 'org.springframework.boot:spring-boot-devtools'
}

bootJar {
    getArchiveFileName().set(provider {
        'et-cos.jar'
    })

    manifest {
        attributes('Implementation-Version': project.version.toString())
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

bootWithCCD {
    // Simulates idam and s2s locally.
    // See rse-cft-lib/LibRunnerTask
    authMode = uk.gov.hmcts.rse.AuthMode.Local
    environment 'RSE_LIB_ADDITIONAL_DATABASES', 'et_cos,et_msg_handler,evidence'

    // ccd-data-store-api
    environment 'DATA_STORE_S2S_AUTHORISED_SERVICES', 'ccd_gw,ccd_data,ccd_ps,aac_manage_case_assignment,ccd_case_document_am_api,am_role_assignment_service,et_cos,et_msg_handler,et_sya_api'
    environment 'CCD_DOCUMENT_URL_PATTERN', '.*'

    // ccd-definition-store-api
    environment 'WELSH_TRANSLATION_ENABLED', 'false'

    // ccd-case-document-am-api
    environment 'DM_STORE_BASE_URL', 'http://localhost:5005'
    environment 'CASE_DOCUMENT_S2S_AUTHORISED_SERVICES', 'ccd_case_document_am_api,ccd_gw,xui_webapp,ccd_data,et_cos,et_sya_api'

    // xui-manage-cases
    environment 'XUI_DOCUMENTS_API', 'http://host.docker.internal:5005'
    environment 'XUI_DOCUMENTS_API_V2', 'http://host.docker.internal:4455'
    environment 'XUI_LD_ID', System.getenv('XUI_LD_ID')

    // xui-manage-org
    environment 'RD_PROFESSIONAL_API_URL', 'http://host.docker.internal:8765'
    environment 'XUI_MANAGE_ORGANISATIONS_CASETYPES', 'ET_EnglandWales,ET_Scotland'

    // et-ccd-callbacks
    //environment 'SERVICEBUS_FAKE', 'true'
}
